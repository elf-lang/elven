projectiles = {}

add_projectile = fun(pos,vel) ? {
	projetile := {}
	projetile.pos 		= pos
	projetile.vel 		= vel
	projetile.timer 	= 0
	projectiles:add(projetile)
	--> projetile
}

projectile_image := get_image_assets("projectile")

draw_projectiles = fun() ? {
	WINDOW:set_src_image(projectile_image)
	for projectile = projectiles[...] ? {
		WINDOW:draw_image(projectile.pos.x,projectile.pos.y)
	}
}

tick_projectiles = fun(time) ? {
	new_projectiles := {}
	for projectile = projectiles[...] ? {
		projectile.pos.x += projectile.vel.x

		keep := true

		if check_tile_solid(projectile.pos) ? {
			keep = false
			add_bullet_hit_wall_effect(projectile.pos,projectile.vel:norm(),4)
		} else if projectile.timer > 360 ? {
			keep = false
		} else {
			if abs(player.dashing) < 50 ? {
				if player.body:contains(projectile.pos) ? {
					add_bullet_hit_entity_effect(player.position+player.size*0.5)
					player_dead()
					keep = false
					VIEW.screen_shake = max(VIEW.screen_shake,16)
				}
			}
		}
		if keep ? {
			new_projectiles:add(projectile)
		}
		projectile.timer += 1
	}
	projectiles = new_projectiles
}