grass_tiles = {}
grass_images := get_image_assets("grass")

tile_size := get_tile_size()

place_grass_tile = fun(pos) ? {
	key := get_tile_key(pos.x,pos.y)
	if grass_tiles[key] ? {
		--> ;
	}
	blades := {}
	num_blades := ntoi(2 + random() * 5)
	for i = 0...num_blades ? {
		blade = {
			force = 0,
			type = (random() * 9999) % grass_images:tally(),
			offset = random()
		}
		blades:add(blade)
	}
	grass_tiles[key] = {
		pos = pos:clone(),
		blades = blades
	}
}

draw_grass_tiles = fun() ? {
	for tile = grass_tiles[...] ? {
		if tile == nil ? {
			continue
		}
		tile_pos := vec2(tile.pos.x,tile.pos.y) * tile_size
		for blade = tile.blades[...] ? {
			image := grass_images:idx(blade.type)
			pos := tile_pos + vec2(tile_size*0.5 + (blade.offset*2-1)*tile_size*0.35,0)
			WINDOW:set_rotation(PI*0.10 * sin(total_time * 0.1 + blade.offset * PI))
			WINDOW:set_center(4,0)
			WINDOW:set_src_image(image)
			WINDOW:draw_image(pos.x,pos.y)
		}
	}
	WINDOW:set_rotation(0)
}